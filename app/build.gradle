apply plugin: 'checkstyle'
apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

ext {
    androidTestVer = "0.5"
    buildToolsVer = "27.0.3"
    butterknifeVer = "8.8.1"
    checkstyleVer = '7.7'
    compileSdkVer = 26
    constraintVer = "1.1.0"
    crashAnalytics = "2.9.1"
    dexMakerVer = "1.2"
    espContVer = "2.2.2"
    espCoreVer = "2.2.2"
    espIntVer = "2.2.2"
    espUIAVer = "2.1.2"
    firebaseVer = "15.0.0"
    flexboxVer = "0.3.1"
    glideVer = "4.0.0-RC0"
    playServicesVer = firebaseVer
    hamcrestVer = "1.3"
    jacocoVer = "0.7.9"
    junitVer = "4.12"
    lapismSearchVer = "5.0.0-alpha7"
    minSdkVer = 21
    mockitoVer = "2.9.0"
    roboElecVer = "3.4.2"
    rxJavaVer = "1.3.0"
    rxJava2Ver = "2.1.6"
    supportVer = "27.1.1"
    targetSdkVer = 26
    verCode = 9
    verName = "1.7-beta"
}

def configDir = "${project.rootDir}/config/"

android {
    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer
    defaultConfig {
        applicationId "com.macbitsgoa.ard"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode verCode
        versionName verName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resValue "string", "APP_VERSION_NAME", "${verCode}-${verName}-${getDate()}"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            testCoverageEnabled true
            versionNameSuffix '-debug'
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexKeepFile file("${configDir}/multidex/multidex.txt")
        }
    }
    dexOptions {
        preDexLibraries = false
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        abortOnError true
        htmlReport true
        ignoreWarnings false
        lintConfig file("$configDir/lint/lint.xml")
        xmlReport true
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jvmArgs '-noverify'
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file("club-keystore-debug.jks")
            storePassword "android"
            keyAlias "debug"
            keyPassword "android"
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res/layout/activities',
                    'src/main/res/layout/viewholders',
                    'src/main/res/layout/fragments',
                    'src/main/res/layout',
                    'src/main/res'
            ]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${supportVer}"
    implementation "com.android.support:support-v4:${supportVer}"
    implementation "com.android.support:design:${supportVer}"
    implementation "com.android.support:customtabs:${supportVer}"
    implementation "com.android.support.constraint:constraint-layout:${constraintVer}"
    implementation "com.android.support:cardview-v7:${supportVer}"
    implementation "com.android.support:support-compat:${supportVer}"

    implementation "com.google.firebase:firebase-core:${firebaseVer}"
    implementation "com.google.firebase:firebase-database:${firebaseVer}"
    implementation "com.google.firebase:firebase-storage:${firebaseVer}"
    implementation "com.google.firebase:firebase-auth:${firebaseVer}"
    implementation "com.google.android.gms:play-services-auth:${playServicesVer}"
    implementation "com.google.firebase:firebase-messaging:${firebaseVer}"
    implementation "com.google.android:flexbox:${flexboxVer}"
    implementation "com.crashlytics.sdk.android:crashlytics:${crashAnalytics}"
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'

    implementation "com.jakewharton:butterknife:${butterknifeVer}"
    implementation "com.github.bumptech.glide:glide:${glideVer}"
    implementation "com.lapism:searchview:${lapismSearchVer}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVer}"
    annotationProcessor "com.github.bumptech.glide:compiler:${glideVer}"

    implementation "io.reactivex.rxjava2:rxjava:${rxJava2Ver}"

    //Unit test compiles
    testImplementation "junit:junit:${junitVer}"
    testImplementation "org.mockito:mockito-core:${mockitoVer}"
    testImplementation("org.robolectric:robolectric:${roboElecVer}") {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }

    //Android test compiles
    androidTestImplementation "com.google.dexmaker:dexmaker:${dexMakerVer}"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:${dexMakerVer}"
    androidTestImplementation "org.hamcrest:hamcrest-library:${hamcrestVer}"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:${espCoreVer}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:${espContVer}") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }

    androidTestImplementation("com.android.support.test:runner:${androidTestVer}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation("com.android.support.test:rules:${androidTestVer}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation("com.android.support.test.espresso:espresso-intents:${espIntVer}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation("com.android.support.test.uiautomator:uiautomator-v18:${espUIAVer}", {
        exclude group: 'com.android.support', module: 'support-annotations'

    })

}

static def getDate() {
    new Date().format('yyyyMMdd')
}

checkstyle {
    toolVersion = checkstyleVer
}

jacoco {
    toolVersion = jacocoVer
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
    }
}

//clean.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile project(':').file("$configDir/checkstyle/checkstyle.xml")
    //configProperties = ["suppressionFile": project(':').file("${configDir}/checkstyle/suppressions.xml")]

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/**/*.java'
    exclude '**/androidTest/**/*.java'

    classpath = files()
    reports {
        xml.enabled true
        html.enabled true
    }
}

task coverage(type: JacocoReport, dependsOn: ['connectedCheck']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      'android/**/*.*',
                      'io/realm/**/*.*',
                      '**/*$ViewInjector*.*',
                      '**/*$ViewBinder*.*',
                      '**/*_ViewBinding.class',
                      '**/*$*.class',
                      '**/*$Lambda$*.*',
                      'com/macbitsgoa/ard/models/**/*.*',
                      'com/macbitsgoa/ard/keys/**/*.*',
                      'com/macbitsgoa/ard/types/**/*.*',
                      'com/macbitsgoa/ard/utils/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])
    doLast {
        delete "build/reports/coverage/"
        file("build/reports/jacoco").renameTo(file("build/reports/coverage"))
    }
}

apply plugin: 'com.google.gms.google-services'
